plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "school.yandex.todolist"
        minSdk 24
        targetSdk 32
        versionCode 1
        versionName "1.0"
        manifestPlaceholders = [YANDEX_CLIENT_ID: "042807fe6c7a4e588ec19faf0c0e561c"]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    testOptions {
        animationsDisabled = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        viewBinding true
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "todolist-${variant.buildType.name}-${defaultConfig.versionName}.apk"
        }
    }
    buildTypes.each {
        it.buildConfigField 'String', 'YANDEX_APP_METRICA', YANDEX_APP_METRICA
    }
}

dependencies {

    implementation "androidx.core:core-ktx:$android_core_version"
    implementation "androidx.appcompat:appcompat:$app_compat_version"

    // Material
    implementation "com.google.android.material:material:$material_design_version"

    // Swipe refresh
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$swipe_refresh_version"

    // Constraint layout
    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout_version"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation platform("com.squareup.okhttp3:okhttp-bom:$okhttp3_version")
    implementation "com.squareup.okhttp3:okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // work manager
    implementation "androidx.work:work-runtime-ktx:$work_manager_version"

    // dagger
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    // yandex auth
    implementation "com.yandex.android:authsdk:$yandex_auth_version"

    // jwt
    implementation "com.auth0.android:jwtdecode:$jwt_version"

    // lottie animations
    implementation "com.airbnb.android:lottie:$lottie_version"

    // yandex app metrica
    implementation "com.yandex.android:mobmetricalib:$yandex_app_metrica_version"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}